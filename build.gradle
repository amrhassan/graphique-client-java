apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.7
version = '1.1-SNAPSHOT'
group = "io.github.amrhassan"
archivesBaseName = "graphique-client"

repositories {
    mavenLocal()
    mavenCentral()
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

dependencies {
    compile 'com.google.guava:guava:18.0'
    compile 'org.apache.httpcomponents:httpclient:4.3.6'
    compile 'commons-io:commons-io:2.4'
    compile 'org.javatuples:javatuples:1.2'
    compile 'org.apache.commons:commons-lang3:3.3.2'
    testCompile "junit:junit:4.11"
}

uploadArchives {

    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Graphique Client'
                packaging 'jar'
                description 'A JVM client for Graphique'
                url 'https://github.com/amrhassan/graphique-client-java/'

                scm {
                    url 'https://github.com/amrhassan/graphique'
                }

                licenses {
                    license {
                        name 'MIT'
                        url 'http://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'amrhassan'
                        name 'Amr Hassan'
                        email 'amr.hassan@gmail.com'
                    }
                }
            }
        }
    }
}
